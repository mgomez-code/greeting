contract Greeter =
  record state = {  // Valores claves a guardar
    greeter_oracle : oracle(string, string),
    greets         : map(address, oracle_query(string, string))}

  stateful entrypoint init() : state =  //Función de Inicio
    let greeter_oracle : oracle(string, string) = register_oracle()
    { greeter_oracle = greeter_oracle, greets = {}  }

  entrypoint get_oracle(): oracle(string, string) =  //Función consultar dirección del oráculo
    state.greeter_oracle

  entrypoint get_balance() : int =  //Función consultar balance del oráculo
    Contract.balance

  stateful entrypoint register_oracle() : oracle(string, string) =  //Función registrar el oráculo
    Oracle.register(Contract.address, 10, RelativeTTL(200))

  entrypoint queryFee() : int =  //Consulta pago mínimo para una consulta del oráculo
    Oracle.query_fee(state.greeter_oracle)

  payable stateful entrypoint greet_oracle(message : string) : bool =  //Función pagar consulta del oráculo
    let val = Call.value
    if(val != 10)
      false
    else
      let query : oracle_query(string, string) =
        Oracle.query(state.greeter_oracle, message, 10, RelativeTTL(50), RelativeTTL(50))
      put(state{greets[Call.caller] = query })
      respond_to_greet()
      true

  entrypoint get_address(direccion : address) : oracle_query(string, string) =  //Función consulta id de consulta
    switch(Map.lookup(direccion, state.greets))
      None    => abort("No Resitrado")
      Some(x) => x

  stateful entrypoint respond_to_greet() =  //Función responder la consulta del oráculo
    let query = get_address(Call.caller)
    let greet : string = Oracle.get_question(state.greeter_oracle, query)
    let greet_is_valid : bool = greet == "Hello" || greet == "Hi" || greet == "Greetings"
    if(greet_is_valid)
      let response : string = greet
      let respuesta : string = String.concat(greet, " Friend") 
      Oracle.respond(state.greeter_oracle, query, respuesta)
    else 
       Oracle.respond(state.greeter_oracle, query, "")
 
  stateful entrypoint get_to_greet() =    //Función mostrar la respuesta del oráculo
    let query = get_address(Call.caller)
    Oracle.get_answer(state.greeter_oracle,query)

  //Funciones básicas
  
  entrypoint contract_creator() = //dirección que creo el contracto
    Contract.creator

  entrypoint contract_address() = //dirección del contracto
    Contract.address

  entrypoint contract_balance() = //balance del contracto
    Contract.balance
